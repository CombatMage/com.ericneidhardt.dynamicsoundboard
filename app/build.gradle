apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

//apply from: '../jacoco.gradle'

android {

	compileSdkVersion rootProject.ext.compileSdkVersion
	buildToolsVersion rootProject.ext.buildToolsVersion

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	defaultConfig {
		applicationId "org.neidhardt.dynamicsoundboard"
		minSdkVersion 16
		targetSdkVersion 23
		versionCode 22300
		versionName "2.23.0"
		resConfigs "de"
	}

	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

	signingConfigs {
		release {
			storeFile file(RELEASE_STORE_FILE)
			storePassword RELEASE_STORE_PASSWORD
			keyAlias RELEASE_KEY_ALIAS
			keyPassword RELEASE_KEY_PASSWORD
		}
	}

	lintOptions {
		abortOnError false
		checkReleaseBuilds false
		disable 'PrivateResource'
	}

	applicationVariants.all { variant ->
		variant.mergeAssets.doFirst {
			File fonts = file("${projectDir}/build/intermediates/exploded-aar/com.github.johnkil.android-robototextview/robototextview/2.4.2/assets/fonts")
			if (fonts.exists()) {
				for (File file : fonts.listFiles()) {
					if (file.getName().contains("RobotoSlab")) {
						println("delete " + file.getName() + " font")
						file.delete()
					};
				}
			}
		}
	}

	buildTypes {
		release {
			signingConfig signingConfigs.release
			minifyEnabled false
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
		debug {
			applicationIdSuffix ".debug"
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		debug.java.srcDirs += 'src/debug/kotlin'
		release.java.srcDirs += 'src/release/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])

	compile project(':safe_async_task')
	compile project(':drag_drop_recycler_view')
	compile project(':enhanced_handler')

	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

	compile 'com.android.support:support-v4:23.2.0'
	compile 'com.android.support:support-v13:23.2.0'
	compile 'com.android.support:appcompat-v7:23.2.0'
	compile 'com.android.support:recyclerview-v7:23.2.0'
	compile 'com.android.support:design:23.2.0'

	compile 'org.greenrobot:eventbus:3.0.0'
	compile 'de.greenrobot:greendao:2.1.0'
	compile 'de.greenrobot:java-common:2.3.1'

	compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
	compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.7.2'

	compile 'com.github.clans:fab:1.6.2'

	compile 'com.github.johnkil.android-robototextview:robototextview:2.5.0'

	compile 'me.zhanghai.android.materialprogressbar:library:1.1.5'

	compile 'jp.wasabeef:recyclerview-animators:2.2.1'

	compile 'ch.acra:acra:4.8.3'

	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:2.0.41-beta'

	testCompile 'org.robolectric:shadows-support-v4:3.0'
	testCompile ('org.robolectric:robolectric:3.0') {
		exclude group: 'commons-logging', module: 'commons-logging'
		exclude group: 'org.apache.httpcomponents', module: 'httpclient'
	}

}


