apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

//apply from: '../jacoco.gradle'

android {

	compileSdkVersion rootProject.ext.compileSdkVersion
	buildToolsVersion rootProject.ext.buildToolsVersion

	useLibrary 'org.apache.http.legacy'

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	defaultConfig {
		applicationId "org.neidhardt.dynamicsoundboard"
		minSdkVersion 16
		targetSdkVersion 23
		versionCode 21604
		versionName "2.16.4"
	}


	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

	signingConfigs {
		release {
			storeFile file(RELEASE_STORE_FILE)
			storePassword RELEASE_STORE_PASSWORD
			keyAlias RELEASE_KEY_ALIAS
			keyPassword RELEASE_KEY_PASSWORD
		}
	}

	lintOptions {
		abortOnError false
		checkReleaseBuilds false
	}

	applicationVariants.all { variant ->
		variant.mergeAssets.doFirst {
			File fonts = file("${projectDir}/build/intermediates/exploded-aar/com.github.johnkil.android-robototextview/robototextview/2.4.2/assets/fonts")
			if (fonts.exists()) {
				for (File file : fonts.listFiles()) {
					if (file.getName().contains("RobotoSlab")) {
						println("delete " + file.getName() + " font")
						file.delete()
					};
				}
			}
		}
	}

	buildTypes {
		release {
			signingConfig signingConfigs.release
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			//proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		debug {
			applicationIdSuffix ".debug"
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		debug.java.srcDirs += 'src/debug/kotlin'
		release.java.srcDirs += 'src/release/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
		testDebug.java.srcDirs += 'src/testDebug/kotlin'
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])

	compile project(':safe_async_task')
	compile project(':drag_drop_recycler_view')

	compile 'com.android.support:support-v4:23.0.1'
	compile 'com.android.support:support-v13:23.0.1'
	compile 'com.android.support:appcompat-v7:23.0.1'
	compile 'com.android.support:recyclerview-v7:23.0.1'
	compile 'com.android.support:design:23.0.1'

	compile 'de.greenrobot:eventbus:2.4.0'
	compile 'de.greenrobot:greendao:2.0.0'

	compile 'ch.acra:acra:4.6.2'
	compile 'com.fasterxml.jackson.core:jackson-databind:2.6.2'
	compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.6.2'

	compile 'com.melnykov:floatingactionbutton:1.3.0'
	compile 'com.github.johnkil.android-robototextview:robototextview:2.4.2'
	compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
	compile 'jp.wasabeef:recyclerview-animators:1.3.0'

	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:1.9.5'

	testCompile 'org.robolectric:shadows-support-v4:3.0'
	testCompile ('org.robolectric:robolectric:3.0') {
		exclude group: 'commons-logging', module: 'commons-logging'
		exclude group: 'org.apache.httpcomponents', module: 'httpclient'
	}

}


