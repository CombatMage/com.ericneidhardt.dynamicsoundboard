apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'kotlin-android'

apply from: '../jacoco.gradle'

android {
	compileSdkVersion 22
	buildToolsVersion "22.0.1"

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	defaultConfig {
		applicationId "org.neidhardt.dynamicsoundboard"
		minSdkVersion 16
		targetSdkVersion 22
		versionCode 21306
		versionName "2.13.06"
	}

	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

	signingConfigs {
		release {
			storeFile file(RELEASE_STORE_FILE)
			storePassword RELEASE_STORE_PASSWORD
			keyAlias RELEASE_KEY_ALIAS
			keyPassword RELEASE_KEY_PASSWORD
		}
	}

	lintOptions {
		abortOnError false
		checkReleaseBuilds false
	}

	applicationVariants.all { variant ->
		variant.mergeAssets.doFirst {
			File fonts = file("${projectDir}/build/intermediates/exploded-aar/com.github.johnkil.android-robototextview/robototextview/2.4.0/assets/fonts")
			if (fonts.exists()) {
				for (File file : fonts.listFiles()) {
					if (file.getName().contains("RobotoSlab")) {
						println("delete " + file.getName() + " font")
						file.delete()
					};
				}
			}
		}
	}

	buildTypes {
		release {
			signingConfig signingConfigs.release
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
		debug {
			applicationIdSuffix ".debug"
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
		testDebug.java.srcDirs += 'src/testDebug/kotlin'
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])

	compile project(':safe_async_task')
	compile project(':drag_drop_recycler_view')

	compile 'com.android.support:support-v4:22.2.0'
	compile 'com.android.support:support-v13:21.0.3'
	compile 'com.android.support:recyclerview-v7:21.0.3'
	compile 'com.android.support:appcompat-v7:22.2.0'

	compile 'de.greenrobot:eventbus:2.4.0'
	compile 'de.greenrobot:greendao:1.3.7'

	compile 'ch.acra:acra:4.5.0'
	compile 'com.fasterxml.jackson.core:jackson-databind:2.5.1'
	compile 'com.melnykov:floatingactionbutton:1.3.0'
	compile 'com.github.johnkil.android-robototextview:robototextview:2.4.0'
	compile 'com.github.castorflex.smoothprogressbar:library:1.0.0'

	compile 'com.google.dagger:dagger:2.0'
	apt 'com.google.dagger:dagger-compiler:2.0'
	provided 'org.glassfish:javax.annotation:10.0-b28'

	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:1.9.5'

	testCompile 'org.robolectric:shadows-support-v4:3.0-rc2'
	testCompile ('org.robolectric:robolectric:3.0-rc2') {
		exclude group: 'commons-logging', module: 'commons-logging'
		exclude group: 'org.apache.httpcomponents', module: 'httpclient'
	}

}


